apply plugin: 'com.android.library'
apply plugin: 'com.amazonaws.appsync'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: "org.owasp.dependencycheck"

node {
    version = "16.18.0"
}

android {
    namespace 'com.sudoplatform.sudodirelay'
    compileSdk 33

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        versionCode = 1
        versionName = "3.1.1"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        testApplicationId = "com.sudoplatform.sudodirelay.test"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = 17
    }

    resourcePrefix 'sen_'

    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
        pickFirst("META-INF/kotlinx-coroutines-core.kotlin_module")
        pickFirst("META-INF/sudodirelay_debug.kotlin_module")
        pickFirst("META-INF/INDEX.LIST")
        pickFirst("META-INF/DEPENDENCIES")
        pickFirst("META-INF/io.netty.versions.properties")
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.all {
            outputs.upToDateWhen { false }
            jacoco {
                includeNoLocationClasses = false
            }
        }
    }
}

ext {
    coroutine_version = "1.7.1"
    kotlintest_version = "3.4.2"
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    // AWS
    //
    // AWS SDKs do not follow semantic versioning rules so we need to strictly specify AWS SDK versions.
    // Follow the process at https://anonyome.atlassian.net/wiki/spaces/PLA/pages/840073287/AWS+Android+SDK+versions+to+use+for+platform+SDKs
    // for updating AWS SDK versions.
    implementation "com.amazonaws:aws-android-sdk-core:$aws_version!!"
    implementation "com.amazonaws:aws-android-sdk-appsync:$appsync_version!!"
    constraints {
        implementation("com.squareup.okhttp3:okhttp:4.9.3") {
            because("Versions < 4.9.3 has CVE-2021-0341")
        }
    }
    // Sudo Platform
    implementation "com.sudoplatform:sudokeymanager:6.0.0"
    implementation "com.sudoplatform:sudologging:3.0.0"
    implementation "com.sudoplatform:sudoconfigmanager:6.0.0"
    implementation "com.sudoplatform:sudouser:17.0.0"
    implementation "com.sudoplatform:sudoapiclient:8.0.0"
    implementation "com.sudoplatform:sudoprofiles:11.0.1"
    implementation 'com.sudoplatform:sudoentitlements:8.0.1'
    // Included as a resolution to fix a CVE. Junit is a transitive of a
    // spongeycastle library from sudokeymanager.
    // TOOD: Can and should be moved to testImplementation once
    // this dependency is resolved.
    implementation("junit:junit:4.13.2")


    // Testing
    testImplementation "io.kotlintest:kotlintest-assertions:$kotlintest_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.mockito:mockito-core:4.7.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.1.0"
    testImplementation "androidx.test:core:1.5.0"
    testImplementation 'org.robolectric:robolectric:4.9'
    testImplementation "com.jakewharton.timber:timber:5.0.1"
    testImplementation 'com.android.support.test:runner:1.0.2'

    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "io.kotlintest:kotlintest-assertions:$kotlintest_version"
    androidTestImplementation "com.jakewharton.timber:timber:5.0.1"
    androidTestImplementation "org.awaitility:awaitility:3.1.6"
    androidTestImplementation "com.sudoplatform:sudoentitlementsadmin:4.0.0"

    androidTestImplementation platform('software.amazon.awssdk:bom:2.15.7')
}

// Returns a list of `configurations` which should not be scanned for vulnerabilities
def getConfigurationsToSkip = { configs ->
    configs.findAll {
        it.name.startsWithAny("androidTest", "test", "lint") ||
                it.name.contains("AndroidTest") ||
                it.name.contains("UnitTest") ||
                it.name.contains("dokka") ||
                it.name.contains("intellij")
    }.collect {
        it.name
    }
}

tasks.register("printConfigurationsToSkip") {
    group = "OWASP dependency-check"
    description = "Print the configurations which are skipped when scanning for vulnerable dependencies"
    doLast {
        println "Configurations to skip when scanning for vulnerable dependencies:"
        println getConfigurationsToSkip(configurations).join("\n")
    }
}

afterEvaluate {
    // NOTE: this must be within `afterEvaluate` to ensure all the configurations have been created before filtering them
    dependencyCheck {
        suppressionFile file("../dependency-suppression.xml").toString()
        failBuildOnCVSS 0
        skipConfigurations = getConfigurationsToSkip(configurations)

        cve {
            urlModified = "https://anonyome-nist-cve-mirror.s3.amazonaws.com/nvdcve-1.1-modified.json.gz"
            urlBase = "https://anonyome-nist-cve-mirror.s3.amazonaws.com/nvdcve-1.1-%d.json.gz"
            startYear = 2002
            waitTime = 0
        }

        analyzers {
            assemblyEnabled = false
            ossIndex {
                username = "${ossIndexUsername}"
                password = "${ossIndexPassword}"
                warnOnlyOnRemoteErrors = true
            }
        }
    }
}

// https://github.com/cookpad/license-tools-plugin
apply plugin: "com.cookpad.android.licensetools"
licenseTools {
    licensesYaml = file("licenses.yml")
    ignoredGroups = ["com.nhaarman:mockito-kotlin", "com.anonyome", "com.sudoplatform"]
    ignoredProjects = ["org.mockito"]
}

@Grab('org.yaml:snakeyaml:1.17')
// Yaml parser
import org.yaml.snakeyaml.Yaml

task checkLicenseHaveRequiredFields {
    description = "Check every license entry has the mandatory fields"
    group = "verification"

    doLast {
        def mandatoryFields = ["artifact", "name", "copyrightHolder", "url"]
        Yaml parser = new Yaml()
        def licenses = parser.load((file("licenses.yml") as File).text)
        def errors = new StringBuilder()
        licenses.each { artifact ->
            mandatoryFields.each { field ->
                if (artifact[field] == null) {
                    errors.append("License entry '${artifact.artifact}' is missing required element `$field`\n")
                }
            }
            if (artifact["license"] == null && artifact["licenseUrl" == null]) {
                errors.append("License entry '${artifact.artifact}' must have one of [license,licenseUrl]\n")
            }
        }
        if (errors.length() > 0) {
            throw new GradleException("Missing license elements:\n$errors")
        }
    }
}
checkLicenses.finalizedBy checkLicenseHaveRequiredFields

// Kotlin Lint
apply plugin: 'org.jmailen.kotlinter'

kotlinter {
    ignoreFailures = false
    reporters = ['checkstyle', 'plain']
    experimentalRules = false
}

// Apollo Code Generation
apollo {
    outputPackageName = "com.sudoplatform.sudodirelay.graphql"
}

// Jacoco test coverage
apply plugin: 'jacoco'
// jacoco parser error in versions below 0.8.7:
jacoco {
    toolVersion = "$jacoco_version"
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"
    classDirectories.setFrom(fileTree(
            dir: "${buildDir}/tmp/kotlin-classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )
    )
    sourceDirectories.setFrom(files("${buildDir.parent}/sudodirelay/src/main/java"))
    // TODO - *.exec file should be generated to buildDir dir, not projectDir
    executionData.setFrom(fileTree(["dir": "$projectDir", "includes": ["**/*.exec", "**/*.ec"]]))
    reports {
        xml {
            required.set(true)
        }
        html {
            required.set(true)
        }
    }
}

// Dokka documentation generation
apply plugin: 'org.jetbrains.dokka'

dokkaHtml {
    outputDirectory.set(project.file("$buildDir/javadoc"))
    dokkaSourceSets {
        configureEach {
            jdkVersion.set(8)
            skipEmptyPackages.set(true)
            includes.from(files("packages.md"))
            samples.from(files("src/test/java/com/sudoplatform/sudodirelay/samples/Samples.kt"))
        }
    }
}

// Sonarqube code analysis
if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
    ext.sonarProjectKey = "sudo-di-relay-android"
    ext.sonarProjectName = "sudo-di-relay-android"
    // todo, patch/* exclusion is temporary measure until aforementioned aws PR is merged
    ext.sonarExclusions = ["*.png", "*.jks", "*.json", "*.key", "src/main/patch/*"]
    apply from: "${rootProject.projectDir}/util/sonarqube.gradle"
}

// Setup common publishing variables
ext {
    projectArtifact = project.name
    projectDescription = 'Sudo Decentralized Identity Relay SDK for the Sudo Platform by Anonyome Labs.'
    projectUrl = 'https://github.com/sudoplatform/sudo-di-relay-android'
    projectSCM = 'scm:git:github.com/sudoplatform/sudo-di-relay-android.git'
    if (project.findProperty("tag")) {
        projectVersion = "$tag"
    } else {
        projectVersion = project.android.defaultConfig.versionName
    }
}

// Internal and External publishing
if (project.findProperty("tag") &&
        project.file("${rootProject.projectDir}/util/publish-mavencentral.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/publish-mavencentral.gradle"
} else if (project.file("${rootProject.projectDir}/util/publish-internal-android.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/publish-internal-android.gradle"
}
